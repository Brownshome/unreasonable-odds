plugins {
	id 'java-library'
	id 'maven-publish'
}

group = 'brownshome'
version = '0.1.0-SNAPSHOT'

allprojects {
	repositories {
		mavenLocal()

		mavenCentral()

		maven {
			url = 'https://maven.pkg.github.com/Brownshome/Vecmath'
			credentials {
				username project.properties['gpr.usr']
				password project.properties['gpr.key']
			}
		}

		maven {
			url = 'https://maven.pkg.github.com/Brownshome/logging'
			credentials {
				username project.properties['gpr.usr']
				password project.properties['gpr.key']
			}
		}

		maven {
			url = 'https://maven.pkg.github.com/Brownshome/game-netcode'
			credentials {
				username project.properties['gpr.usr']
				password project.properties['gpr.key']
			}
		}
	}

	tasks.withType(JavaCompile) {
		options.encoding 'utf-8'
		options.compilerArgs += ['--enable-preview']
	}
}

java {
	withJavadocJar()
	withSourcesJar()
}

dependencies {
	api 'brownshome:vecmath:[3.5.0, 4.0.0)'
	api 'brownshome:netcode:0.1.0-SNAPSHOT'

	compileOnly 'brownshome:netcode-annotations:0.1.0-SNAPSHOT'
	annotationProcessor 'brownshome:netcode-processor:0.1.0-SNAPSHOT'

	runtimeOnly 'brownshome:logging-slf4j:1.0.1'
}

publishing {
	repositories {
		maven {
			name = 'GitHubPackages'
			url = uri('https://maven.pkg.github.com/Brownshome/unreasonable-odds')
			credentials {
				username project.properties['gpr.usr']
				password project.properties['gpr.key']
			}
		}
	}

	publications {
		mavenJava(MavenPublication) {
			artifactId 'unreasonable-odds'
			from components.java

			pom {
				name = 'Unreasonable Odds'
				description = 'The core gameplay and logic for Unreasonable Odds'
				url = 'https://github.com/Brownshome/unreasonable-odds'
				developers {
					developer {
						id = 'Brownshome'
						name = 'James Brown'
						email = 'jamesphone180@gmail.com'
					}
				}
				scm {
					connection = 'scm:git:git://github.com/Brownshome/unreasonable-odds.git'
					url = 'https://github.com/Brownshome/unreasonable-odds'
				}
			}
		}
	}
}

dependencies {
	testImplementation 'org.junit.jupiter:junit-jupiter-api:[5.0.0, 6.0.0)'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:[5.0.0, 6.0.0)'
}

test {
	useJUnitPlatform()
	enableAssertions true
	jvmArgs += ['--enable-preview']
}